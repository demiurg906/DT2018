\import lect04
\open Nat

-- 1. Определите конгруэнтность для функций двух аргументов через transport.

\func pmap2 {A B C : \Type} (f : A -> B -> C) {a a' : A} (p : a = a') {b b' : B} (q : b = b') : f a b = f a' b'
    => trans
        (transport (\lam y => f a b  = f a y ) q idp) -- f a b = f a b'
        (transport (\lam x => f a b' = f x b') p idp) -- f a b' = f a' b'

-- 2. Докажите, что transport выражается через pmap и repl и наоборот.

-- Определение pmap через transport было дано на лекции.

-- repl говорит, что если у нас есть два равных типа, то у нас есть функция из одного во второй.
-- Определите repl через transport.

\func repl {A B : \Type} (p : A = B) (a : A) : B
    => transport (\lam x => x) p a

-- Определите transport через repl и pmap.
\func transport' {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (b : B a) : B a'
    => repl (pmap B p) b

-- В заданиях 3-6 нельзя использовать не только coe, но и все функции, определенные через него, в частности transport.

-- 3. Мы доказали, что left = right через coe. Докажите это, не используя эту функцию.

\func left=right : left = right
    => path (\lam x => x)

-- 4. Докажите, что если a = a' и b = b', то (a,b) = (a',b'), не используя coe.

\func pairEq {A B : \Type} {a a' : A} {b b' : B} (p : a = a') (q : b = b') : (a,b) = (a',b')
    => path (\lam i => (p @ i, q @ i))

-- 5. Докажите, что если p = p', то p.1 = p'.1, не используя coe.

\func projEq {A : \Type} (B : A -> \Type) (p p' : \Sigma (x : A) (B x)) (t : p = p') : p.1 = p'.1
    => path (\lam i => (t @ i).1)

-- 6. Докажите pmap2, не используя coe.

\func pmap2' {A B C : \Type} (f : A -> B -> C) {a a' : A} (p : a = a') {b b' : B} (q : b = b') : f a b = f a' b'
    => path (\lam i => f (p @ i) (q @ i))

-- 7. Определите функцию B left -> B right.

\func Itr {B : I -> \Type} (b : B left) : B right
    => coe B b right

-- 8. Определите функцию B right -> B left.

\func Itr' {B : I -> \Type} (b : B right) : B left
    => transport B (sym left=right) b

-- 9. Мы видели как определить transport через coe. Определите coe через transport. Взаимообратны ли эти конструкции?

\func coe' (A : I -> \Type) (a : A left) : A right
    => transport A left=right a

-- 10. Докажите, что (\lam x => not (not x)) = (\lam x => x).

\func not (b : Bool) : Bool
  | true => false
  | false => true

\func notNotId : (\lam x => not (not x)) = (\lam x => x)
    => funExt (\lam _ => Bool) (\lam b => not-isInvolution_ b)
  \where \func not-isInvolution_ (x : Bool) : not (not x) = x
           | true => idp
           | false => idp
